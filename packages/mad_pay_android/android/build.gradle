group 'ru.madbrains.mad_pay_android'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.6.21'
    ext.protobuf_version = '0.8.18'
    ext.protobuf_javalite_version = '3.21.1'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobuf_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.proto.srcDirs += 'protos'
    }

    defaultConfig {
        minSdkVersion 21
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_javalite_version"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_javalite_version"
    implementation 'com.google.android.gms:play-services-wallet:19.1.0'
}
